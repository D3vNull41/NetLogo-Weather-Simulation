;==============================
; Procedure: topography-set-globals
; Parameter(s): None
; Description: Initializes global variables defining global-temoperature (init: 0 Â°K)
;==============================
to energy-set-globals
  set global-temperature 0
  set solar-irradiance 0
  set atmospheric-transmittance 0.79 ; Example transmittance value
end

;==============================
; Procedure: energy-init-temperature
; Parameter(s): None
; Description:  Initializes the temperature of each patch to 288 K (equivalent to 15Â°C).
;==============================
to energy-init-temperature
  ask patches [
    set temperature 288 ; 15Â°C
  ]
end

;==============================
; Procedure: energy-calc-temp
; Parameter(s): None
; Description: Calculates the temperature change for each patch based on solar irradiance, energy loss, and heat capacity.
;==============================
to energy-calc-temp
  let heat 0
  let albedo (item 4 soil-properties) ; Albedo (ğ›¼)
  let emissivity (item 5 soil-properties) ; emissivity (ğœ–)
  let specific-heat (item 3 soil-properties) ; Heat capacity (c)
  let density (item 6 soil-properties) ; density (Ï)
  
  ; Calculate reflected solar irradiance
  let reflection 1 - albedo
  let absorbed-energy reflection * solar-irradiance ; (1 âˆ’ ğ›¼) * P(t)
  
  ; Calculate radiative heat loss using Stefan-Boltzmann law
  let radiative-loss energy-calc-energy-loss emissivity temperature ; ğœ– * Ïƒ * T^4
  
  ; Calculate real energy change
  let real-energy absorbed-energy - radiative-loss
  
  ; Adjust heat calculation considering the specific heat capacity and density
  set heat real-energy / (specific-heat * density) ; adjusted heat calculation with density and specific heat capacity
  set temperature temperature + heat ; temperature in K (kelvin)
end


;==============================
; Procedure:  energy-calc-solar-irradiance
; Parameter(s): t - time since sunrise
; Description: set the solar irradiation aproximated as a pure harmonic function of time around an average. Returnd as W/mÂ²
;==============================
to  energy-calc-solar-irradiance [t]
  let daylight-duration 12 ; d
  let solar-constant 1361
  let avg-irradiance solar-constant * atmospheric-transmittance; solar constant without athmosphere in W/m2

  set solar-irradiance avg-irradiance * max list rad-sin (pi / 2 * (1 - (t - (daylight-duration / 2)) / (daylight-duration / 2))) 0 ; always be positiv ;)
end

;==============================
; Procedure: rad-sin
; Parameters: r - Angle in radians
; Description: This procedure calculates the sine of an angle given in radians. It first converts the angle to degrees, computes the sine, and returns the result.
;==============================
to-report rad-sin [rad]
  let angle-degrees rad * 360 / (2 * pi)   ; Convert angle from radians to degrees
  report sin angle-degrees
end

;==============================
; Procedure:  energy-calc-energy-loss
; Parameter(s): emissivity - emissivity of the soil (ğœ–), temperatur - temperatur (T) of the soil in Kelvin (K)
; Description: return the emissivity of the soil by applying the Stefan-Boltzman law
;==============================
to-report energy-calc-energy-loss [emissivity temperatur]
  let humidity-factor calc-humidity-factor relative-humidity
  let surface-area patch-size * patch-size ; expirement with different values...
  let boltzmann-constant 5.670374419e-8 ; Stefan-Boltzmann-constant in W/mÂ²/K^4
  
  report surface-area * emissivity * boltzmann-constant * temperatur ^ 4 * humidity-factor
end

;==============================
; Procedure: calc-humidity-factor
; Parameter(s): relative-humidity
; Description: Calculates the humidity factor based on relative humidity.
;==============================
to-report calc-humidity-factor [_relative-humidity]
  report 1 - 0.3 * (_relative-humidity / 100)
end

;==============================
; Procedure: energy-set-global-temperature
; Parameter(s): None
; Description: Calculates the average temperature of all patches excluding water patches and sets the global temperature in Celsius.
;==============================
to energy-set-global-temperature
  let current-average-temperature energy-average-patch-temperature
  set global-temperature energy-kelvin-to-celsius current-average-temperature
end

;==============================
; Procedure: energy-average-patch-temperature
; Parameter(s): None
; Description: Calculates the average temperature of all patches excluding water patches.
;==============================
to-report energy-average-patch-temperature
  report mean [temperature] of patches 
end

;==============================
; Procedure: energy-kelvin-to-celsius
; Parameter(s): temperatur - Temperature in Kelvin
; Description: Converts temperature from Kelvin to Celsius.
;==============================
to-report energy-kelvin-to-celsius [temperatur]
  report temperatur - 273.15
end
