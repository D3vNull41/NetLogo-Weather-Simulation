;==============================
; Procedure: topography-set-globals
; Parameter(s): None
; Description: Initializes global variables defining global-temoperature (init: 0 ¬∞K)
;==============================
to energy-set-globals
  set global-temperature 0
  set solar-irradiance 0
  set atmospheric-transmittance 0.78 ; Example transmittance value (incoming solar engergy 100 - 6% reflected by the atmosphere - 16% absorbed by the atmosphere)
end

;==============================
; Procedure: energy-init-temperature
; Parameter(s): None
; Description:  Initializes the temperature of each patch to 288 K (equivalent to 15¬∞C).
;==============================
to energy-init-temperature
  ask patches [
    set soil-heat-loss 0
    set temperature 288 ; 15¬∞C
  ]
end

;==============================
; Procedure: energy-calc-temp
; Parameter(s): None
; Description: Calculates the temperature change for each patch based on solar irradiance, energy loss, and heat capacity.
;==============================
to energy-calc-temp
  let heat 0
  let albedo (item 4 soil-properties) ; Albedo (ùõº)
  let emissivity (item 5 soil-properties) ; Emissivity (ùúñ)
  let specific-heat (item 3 soil-properties) ; Specific heat capacity (c)
  let density (item 6 soil-properties) ; Density (œÅ)
  
  ; Calculate reflected solar irradiance
  let reflection (1 - albedo)
  set soil-absorbed-energy (reflection * solar-irradiance) ; (1 ‚àí ùõº) * P(t)
  
  ; Calculate radiative heat loss using Stefan-Boltzmann law (longwave heat emission)
  let radiative-loss (energy-calc-energy-loss emissivity temperature) ; ùúñ * œÉ * T^4 * H_r (H_r: is an improvement to take into account the saturation moisture content)
  set soil-heat-loss radiative-loss
  
  ; Calculate real energy change
  let real-energy (soil-absorbed-energy - radiative-loss)
  set net-radiation real-energy
  
  ; Adjust heat calculation considering the specific heat capacity and density
  ; Note: the specific heat capacity should be used correctly here.
  ;       specific-heat * density gives the volumetric heat capacity (c_v)
  let volumetric-heat-capacity (specific-heat * density)
  
  ; Heat change should be divided by volumetric heat capacity (J/(K*m^3))
  set heat (real-energy / volumetric-heat-capacity) ; adjusted heat calculation with density and specific heat capacity
  
  ; Update temperature
  set temperature (temperature + heat) ; temperature in K (Kelvin)
end


;==============================
; Procedure:  energy-calc-solar-irradiance
; Parameter(s): t - time since sunrise
; Description: set the solar irradiation aproximated as a pure harmonic function of time around an average. Returnd as W/m¬≤
;==============================
to  energy-calc-solar-irradiance [t]
  let daylight-duration 12 ; d
  let solar-constant 1361 ; solar constant without athmosphere in W/m2
  let avg-irradiance solar-constant * atmospheric-transmittance

  set solar-irradiance avg-irradiance * max list rad-sin (pi / 2 * (1 - (t - (daylight-duration / 2)) / (daylight-duration / 2))) 0 ; always be positiv ;)
end

;==============================
; Procedure: rad-sin
; Parameters: r - Angle in radians
; Description: This procedure calculates the sine of an angle given in radians. It first converts the angle to degrees, computes the sine, and returns the result.
;==============================
to-report rad-sin [rad]
  let angle-degrees rad * 360 / (2 * pi)   ; Convert angle from radians to degrees
  report sin angle-degrees
end

;==============================
; Procedure:  energy-calc-energy-loss
; Parameter(s): emissivity - emissivity of the soil (ùúñ), temperatur - temperatur (T) of the soil in Kelvin (K)
; Description: return the emissivity of the soil by applying the Stefan-Boltzman law
;==============================
to-report energy-calc-energy-loss [emissivity temperatur]
  let humidity-factor calc-humidity-factor relative-humidity
  let surface-area patch-size * patch-size ; expirement with different values...
  let boltzmann-constant 5.670374419e-8 ; Stefan-Boltzmann-constant in W/m¬≤/K^4
  
  report surface-area * emissivity * boltzmann-constant * temperatur ^ 4 * humidity-factor
end

;==============================
; Procedure: calc-humidity-factor
; Parameter(s): relative-humidity
; Description: Calculates the humidity factor based on relative humidity.
;==============================
to-report calc-humidity-factor [_relative-humidity]
  report 1 - 0.05 * (_relative-humidity / 100)
end

;==============================
; Procedure: energy-set-global-temperature
; Parameter(s): None
; Description: Calculates the average temperature, average termal radiation and average reflected irradiance of all patches
;==============================
to energy-set-global-temperature
  let current-average-temperature mean [temperature] of patches
  set global-temperature energy-kelvin-to-celsius current-average-temperature
  set avg-termal-radioation mean [soil-heat-loss] of patches
  set avg-reflected-irradiance mean [soil-absorbed-energy] of patches
end

;==============================
; Procedure: energy-kelvin-to-celsius
; Parameter(s): temperatur - Temperature in Kelvin
; Description: Converts temperature from Kelvin to Celsius.
;==============================
to-report energy-kelvin-to-celsius [temperatur]
  report temperatur - 273.15
end
